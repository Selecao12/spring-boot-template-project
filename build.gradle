plugins {
	id 'org.springframework.boot' version '2.6.2'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'com.avast.gradle.docker-compose' version "$gradleDockerComposePlugin"
	id 'org.liquibase.gradle' version '2.1.1'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('testcontainersVersion', "1.16.2")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	liquibaseRuntime 'org.liquibase:liquibase-core'
	liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl'
	liquibaseRuntime 'info.picocli:picocli'
	liquibaseRuntime 'org.postgresql:postgresql'

	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	implementation 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'
}

dependencyManagement {
	imports {
		mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
	}
	dependencies {
		dependency 'org.liquibase:liquibase-core:4.7.0'
		dependency 'org.liquibase:liquibase-groovy-dsl:3.0.2'
		dependency 'info.picocli:picocli:4.6.2'
		dependency 'org.postgresql:postgresql:42.3.1'
	}
}

test {
	useJUnitPlatform()
}

dockerCompose.isRequiredBy(test)

liquibase {
	activities {
		main {
			changeLogFile 'src/main/resources/db/changelog-master.xml'
			url 'jdbc:postgresql://localhost:54321/template?currentSchema=template_schema'
			username 'postgres'
			password '123456'
			driver 'org.postgresql.Driver'
		}
	}
	runList = 'main'
}